WHITESPACE = _{ " " | "\t" }
id_token = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
qualified_field = { id_token ~ "." ~ id_token }
field = { qualified_field | id_token }
field_list = { "(" ~ ( field ) ~ ("," ~ ( field ))* ~ ")" }
select_list = { ( field ) ~ ("," ~ ( field ))* }
table_list = { ( id_token ) ~ ("," ~ ( id_token ))* }
string_content = @{ (!"\'" ~ ANY)* }
string_token = { "\'" ~ string_content ~ "\'" }
int_token = @{ ASCII_DIGIT+ }
constant = { int_token | string_token }
constant_list = { "(" ~ ( constant ) ~ ("," ~ ( constant ))* ~ ")" }
expression = { field | constant }
term = { expression ~ "=" ~ expression }
predicate = { term ~ ("and" ~ term)* }
select = @{ ^"select" ~ !(ASCII_ALPHANUMERIC | "_") }
from = @{ ^"from" ~ !(ASCII_ALPHANUMERIC | "_") }
insert = @{ ^"insert" ~ !(ASCII_ALPHANUMERIC | "_") }
integer = @{ ^"integer" ~ !(ASCII_ALPHANUMERIC | "_") }
text = @{ ^"varchar" ~ !(ASCII_ALPHANUMERIC | "_") }
create = @{ ^"create" ~ !(ASCII_ALPHANUMERIC | "_") }
table = @{ ^"table" ~ !(ASCII_ALPHANUMERIC | "_") }
table_name = @{ (ASCII_ALPHA | "_")+ }
column_name = @{ (ASCII_ALPHA | "_")+ }
select_sql = { SOI ~ select ~ select_list ~ from ~ table_list ~ ( "where" ~ predicate )? ~EOI }
insert_sql = { SOI ~ insert ~ "into" ~ id_token ~ field_list ~ "values" ~ constant_list ~ EOI }
delete_sql = { SOI ~ ^"delete" ~ "from" ~ id_token ~ ( "where" ~ predicate )? ~ EOI }
update_sql = { SOI ~ ^"update" ~ id_token ~ "set" ~ field ~ "=" ~ constant ~ ( "where" ~ predicate )? ~ EOI }
show_tables_sql = { SOI ~ ^"show" ~ "tables" ~ EOI }
describe_table_sql = { SOI ~ ^"describe" ~ id_token ~ EOI }
field_definition = { id_token ~ integer | id_token ~ text ~ "(" ~ int_token ~ ")" }
field_definitions = { "(" ~ ( field_definition ) ~ ("," ~ ( field_definition )) ~ ")" }
create_table_sql = { SOI ~ create ~ table ~ id_token ~ field_definitions ~ EOI }
sql = { select_sql | insert_sql | create_table_sql | delete_sql | update_sql | show_tables_sql | describe_table_sql }
